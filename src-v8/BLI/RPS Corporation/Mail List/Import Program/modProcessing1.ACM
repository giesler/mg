Option Compare Database
Option Explicit


Public Function ProcessTitles(stblName As String) As Boolean
'On Error GoTo PT_Err

' ----- Process include names
Dim rstItems As Recordset, numItems As Integer, i As Integer
Dim strSQL As String

ProcessTitles = False
Set rstItems = New Recordset
rstItems.Open "tblMailInclude", CurrentProject.Connection, adOpenStatic, adLockReadOnly
If rstItems.BOF And rstItems.EOF Then
  MsgBox "There are no include names, function ended.", vbCritical
  rstItems.Close
  Set rstItems = Nothing
  Exit Function
End If
rstItems.MoveLast
numItems = rstItems.RecordCount
rstItems.MoveFirst

Call OpenStatusForm("Processing include names...", numItems)

i = 0
Do While Not rstItems.EOF
  i = i + 1
  strSQL = "UPDATE [" & stblName & "] SET Include = 1 " _
      & "WHERE (Title Like '%" & rstItems!Title & "%')"
  CurrentProject.Connection.Execute strSQL
  UpdateStatusForm i
  rstItems.MoveNext
Loop

rstItems.Close
Set rstItems = Nothing
CloseStatusForm


' remove items not marked to include
Dim intRecs As Long
OpenWaitForm "Removing names not included..."
CurrentProject.Connection.Execute "Delete From [" & stblName & "] Where Include = 0", intRecs
Debug.Print "   Records removed: " & intRecs
CloseWaitForm

' Go through deletes now
Set rstItems = New Recordset
rstItems.Open "tblMailExclude", CurrentProject.Connection, adOpenStatic, adLockReadOnly
If rstItems.BOF And rstItems.EOF Then
    MsgBox "There are no exclude names, function ended.", vbCritical
    rstItems.Close
    Set rstItems = Nothing
    Exit Function
End If
rstItems.MoveLast
numItems = rstItems.RecordCount
rstItems.MoveFirst

Call OpenStatusForm("Deleting exclude names...", numItems)

i = 0
intRecs = 0
Dim intTempRecs As Long
Do While Not rstItems.EOF
  i = i + 1
  strSQL = "Delete From [" & stblName & "] " _
      & "WHERE (Title Like '%" & rstItems!Title & "%')"
  CurrentProject.Connection.Execute strSQL, intTempRecs
  intRecs = intRecs + intTempRecs
  UpdateStatusForm i
  rstItems.MoveNext
Loop
Debug.Print "Exclude names deleted: " & CStr(intRecs)

rstItems.Close
Set rstItems = Nothing
CloseStatusForm
ProcessTitles = True

Debug.Print "Done"

'MsgBox "Title processing completed succesfully.", vbInformation

Exit Function
PT_Err:
Select Case Err
    Case Else
        ErrHand "modStuff", "ProcessTitles"
        Exit Function
End Select

End Function